plugins {
    id 'java-library-distribution'
    id 'maven-publish'
    id 'signing'
}

//group 'io.github.sabroe.topp'
//version '0.0.4-test'

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = 'topp-json-serialization-utility'
            version = project.version
            from components.java
            pom {
                name = 'Topp-JSON-Serialization'
                description = 'Topp JSON Serialization is an open-source Java library which implements utilities related to the serialization of JSON.'
                url = 'https://github.com/sabroe/Topp-JSON'
                inceptionYear = '2022'

                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'https://github.com/sabroe/Topp-JSON/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'sabroe'
                        name = 'Morten Sabroe Mortensen'
                        email = 'morten.sabroe.mortensen@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git:github.com:sabroe/Topp-JSON.git'
                    developerConnection = 'scm:git:ssh://github.com:sabroe/Topp-JSON.git'
                    url = 'https://github.com/sabroe/Topp-JSON'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'OSSRH'
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = project.properties['ossrhUsername']
                password = project.properties['ossrhPassword']
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
